apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.getkeepsafe.dexcount'
apply plugin: 'me.tatarka.retrolambda'

android {
    compileSdkVersion project.targetSdkVersion
    buildToolsVersion project.buildToolsVersion

    defaultConfig {
        minSdkVersion project.minSdkVersion
        targetSdkVersion project.targetSdkVersion
        versionName project.versionName
        vectorDrawables.useSupportLibrary = true

        buildConfigField ("String", "DEFAULT_SEARCH_ENGINE", "\"https://www.bing.com/search?q=\"")
        // see acr.browser.lightning.search.SearchEngineProvider
        buildConfigField ("int", "DEFAULT_SEARCH_ENGINE_INDEX", "1")

        // put the variant name and the version number into the generated apk
        android.applicationVariants.all { variant ->
            def appName
            //Check if an applicationName property is supplied; if not use the name of the parent project.
            if (project.hasProperty("applicationName")) {
                appName = ext.applicationName
            } else {
                appName = parent.name
            }

            variant.outputs.each { output ->
                def newApkName
                //If there's no ZipAlign task it means that our artifact will be unaligned and we need to mark it as such.
                newApkName = "${appName}-${output.baseName}-${variant.versionName}.apk"
                output.outputFile = new File(output.outputFile.parent, newApkName)
            }
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
            proguardFiles 'proguard-project.txt'
        }

        release {
            minifyEnabled false
            shrinkResources false
            proguardFiles 'proguard-project.txt', 'proguard-rules-airfox.pro'
        }
    }

    productFlavors {
        lightningPlus {
            buildConfigField "boolean", "FULL_VERSION", "Boolean.parseBoolean(\"true\")"
            applicationId "acr.browser.lightning"
            versionCode project.versionCode_plus
        }

        lightningLite {
            buildConfigField "boolean", "FULL_VERSION", "Boolean.parseBoolean(\"false\")"
            applicationId "acr.browser.barebones"
            versionCode project.versionCode_lite
        }

        airfox {
            applicationId "com.mgensuite.airfox.browser2"
            versionCode 11
            versionName '1.1.0'

            // because of draw over other apps permission
            targetSdkVersion 22

            multiDexEnabled true

            buildConfigField "boolean", "FULL_VERSION", "Boolean.parseBoolean(\"true\")"

            resValue "string", "contact_email_address", "care@airfox.io"
            resValue "string", "contact_faq_address", "http://www.airtoken.com"

            buildConfigField ("String", "DEFAULT_SEARCH_ENGINE", "\"https://www.bing.com/search?q=\"")
            // see acr.browser.lightning.search.SearchEngineProvider
            buildConfigField ("int", "DEFAULT_SEARCH_ENGINE_INDEX", "3")

            // Facebook SDK
            resValue "string", "facebook_app_id", "1420943194608607"
            resValue "string", "fb_login_protocol_scheme", "fb1420943194608607"
        }
    }

    lintOptions {
        abortOnError true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    retrolambda {
        javaVersion JavaVersion.VERSION_1_6
        defaultMethods false
        incremental true
        jvmArgs '-noverify'
    }

    packagingOptions {
        exclude '.readme'
        exclude 'publicsuffixes.gz'
        exclude 'META-INF/rxjava.properties'
        exclude 'META-INF/LICENSE'
    }
}

dexcount {
    includeClasses = false
    includeFieldCount = false
    format = "tree"
    orderByMethodCount = true
    verbose = false
}

dependencies {
    testCompile 'junit:junit:4.12'

    // AirFox SDK
    compile ("com.mgensuite:airfox-mobile-sdk:$airfox_lib_version") {
        exclude group: 'com.intellij', module: 'annotations'
        exclude group: 'com.squareup.okhttp3', module: 'okhttp'
        transitive = true
        changing = true
    }

    // MultiDex
    compile 'com.android.support:multidex:1.0.2'
    androidTestCompile 'com.android.support:multidex:1.0.2'

    // support libraries
    compile "com.android.support:support-v4:$support_lib_version"
    compile "com.android.support:palette-v7:$support_lib_version"
    compile "com.android.support:appcompat-v7:$support_lib_version"
    compile "com.android.support:design:$support_lib_version"
    compile "com.android.support:recyclerview-v7:$support_lib_version"
    compile "com.android.support:support-v4:$support_lib_version"

    // html parsing for reading mode
    compile 'org.jsoup:jsoup:1.10.3'

    // dependency injection
    def daggerVersion = '2.11'
    compile "com.google.dagger:dagger:$daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"
    provided 'javax.annotation:jsr250-api:1.0'

    // view binding
    def butterknifeVersion = '8.8.1'
    compile "com.jakewharton:butterknife:$butterknifeVersion"
    kapt "com.jakewharton:butterknife-compiler:$butterknifeVersion"

    // permissions
    compile 'com.anthonycr.grant:permissions:1.1.2'

    // proxy support
    compile 'net.i2p.android:client:0.8'

    compile 'com.squareup.okhttp3:okhttp:3.9.0'

    // file reading
    def mezzanineVersion = '1.0.0'
    compile "com.anthonycr.mezzanine:mezzanine:$mezzanineVersion"
    kapt "com.anthonycr.mezzanine:mezzanine-compiler:$mezzanineVersion"

    // tor proxy
    def netcipherVersion = '2.0.0-alpha1'
    compile "info.guardianproject.netcipher:netcipher:$netcipherVersion"
    compile "info.guardianproject.netcipher:netcipher-webkit:$netcipherVersion"

    compile 'com.anthonycr.bonsai:bonsai:1.1.0'

    compile 'com.anthonycr.progress:animated-progress:1.0'

    // memory leak analysis
    def leakCanaryVersion = '1.5.1'
    debugCompile "com.squareup.leakcanary:leakcanary-android:$leakCanaryVersion"
    releaseCompile "com.squareup.leakcanary:leakcanary-android-no-op:$leakCanaryVersion"

    // kotlin
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"

    // progress indicator
    compile 'com.akexorcist:RoundCornerProgressBar:2.0.3'
}

apply plugin: 'com.google.gms.google-services'
